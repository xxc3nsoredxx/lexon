whitespace is handled by lex
i_numeral matches an entire integer
NUMBER is split into INTEGER and FLOAT

JSON    =>  ELEMENT

VALUE   =>  OBJECT
        =>  ARRAY
        =>  STRING
        =>  NUMBER
        =>  "true"
        =>  "false"
        =>  "null"

OBJECT  =>  '{' ws  '}'
        =>  '{' MEMBERS '}'

MEMBERS =>  MEMBER
        =>  MEMBER  ',' MEMBERS

MEMBER  =>  ws  STRING  ws  ':' ELEMENT

ARRAY   =>  '[' ws  ']'
        =>  '[' ELEMENTS    ']'

ELEMENTS    =>  ELEMENT
            =>  ELEMENT ',' ELEMENTS

ELEMENT =>  ws  VALUE   ws

STRING  =>  '"' CHARACTERS  '"'

CHARACTERS  =>  ""
            =>  CHARACTER   CHARACTERS

CHARACTER   =>  '0020'...'10FFFF'-'"'-'\'
            =>  '\' ESCAPE

ESCAPE  =>  '"'
        =>  '\'
        =>  '/'
        =>  'b'
        =>  'f'
        =>  'n'
        =>  'r'
        =>  't'
        =>  'u' HEX HEX HEX HEX

HEX =>  DIGIT
    =>  'A'...'F'
    =>  'a'...'f'

NUMBER  => INTEGER  FRACTION    EXPONENT

INTEGER =>  DIGIT
        =>  ONENINE DIGITS
        =>  '-' DIGIT
        =>  '-' ONENINE DIGITS

DIGITS  =>  DIGIT
        =>  DIGIT   DIGITS

DIGIT   =>  '0'
        =>  ONENINE

ONENINE =>  '1'...'9'

FRACTION    =>  ""
            =>  '.' DIGITS

EXPONENT    =>  ""
            =>  'E' sign    DIGITS
            =>  'e' sign    DIGITS

sign    =>  ""
        =>  '+'
        =>  '-'

ws  =>  ""
    =>  '0020'  ws
    =>  '000A'  ws
    =>  '000D'  ws
    =>  '0009'  ws
